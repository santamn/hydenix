# KDE のファイルマネージャ Dolphin と、そのサムネイル・プロトコル関連の一式。
#
# 注意: `.config/kdeglobals` は qt.nix でも同じ内容で定義されている。
# 属性集合リテラルどうしのマージなのでエラーにはならない
# （treefmt.nix で statix の repeated_keys lint を無効化しているのはこのため）。
{
  config,
  lib,
  pkgs,
  ...
}: let
  cfg = config.hydenix.hm.dolphin;
in {
  options.hydenix.hm.dolphin = {
    enable = lib.mkOption {
      type = lib.types.bool;
      default = config.hydenix.hm.enable;
      description = "Enable dolphin module";
    };
  };

  config = lib.mkIf cfg.enable {
    home.packages = with pkgs.kdePackages; [
      dolphin # KDE file manager
      qtimageformats # Image format support for Qt5
      ffmpegthumbs # Video thumbnail support
      kde-cli-tools # KDE command line utilities
      kdegraphics-thumbnailers # KDE graphics thumbnails
      kimageformats # Additional image format support for KDE
      qtsvg # SVG support
      kio # KDE I/O framework
      kio-extras # Additional KDE I/O protocols
      kwayland # KDE Wayland integration
    ];

    xdg.mimeApps = {
      defaultApplications = {
        "inode/directory" = ["org.kde.dolphin.desktop"];
        "x-scheme-handler/file" = ["org.kde.dolphin.desktop"];
        "x-scheme-handler/about" = ["org.kde.dolphin.desktop"];
      };
    };

    home.file = {
      ".config/dolphinrc" = {
        source = "${pkgs.hyde}/Configs/.config/dolphinrc";
        force = true;
      };
      ".config/baloofilerc" = {
        source = "${pkgs.hyde}/Configs/.config/baloofilerc";
        force = true;
      };
      ".config/menus/applications.menu" = {
        source = "${pkgs.hyde}/Configs/.config/menus/applications.menu";
        force = true;
      };

      # stateful file for themes
      ".config/kdeglobals" = {
        source = "${pkgs.hyde}/Configs/.config/kdeglobals";
        force = true;
        mutable = true;
      };
    };
  };
}
